// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    alias libs.plugins.android.application apply false
    alias libs.plugins.android.library apply false
    alias libs.plugins.android.test apply false
    alias libs.plugins.kotlin.android apply false
    alias libs.plugins.hilt.android apply false
    alias libs.plugins.protobuf apply false
    alias libs.plugins.detekt apply false
    alias libs.plugins.ksp apply false
    alias libs.plugins.paparazzi apply false
    alias libs.plugins.sentry.android apply false
    alias libs.plugins.sentry.kotlin.compiler apply false
    alias libs.plugins.androidx.baselineprofile apply false
    alias libs.plugins.kotlin.plugin.parcelize apply false
    alias libs.plugins.dependency.analysis
}

subprojects {
    plugins.withId("app.cash.paparazzi") {
        // Defer until afterEvaluate so that testImplementation is created by Android plugin.
        afterEvaluate {
            dependencies.constraints {
                add("testImplementation", "com.google.guava:guava") {
                    attributes {
                        attribute(
                                TargetJvmEnvironment.TARGET_JVM_ENVIRONMENT_ATTRIBUTE,
                                objects.named(TargetJvmEnvironment, TargetJvmEnvironment.STANDARD_JVM)
                        )
                    }
                    because("LayoutLib and sdk-common depend on Guava's -jre published variant." +
                            "See https://github.com/cashapp/paparazzi/issues/906.")
                }
            }
        }
    }
}
